//The following query provides the list of all users and count of devices withdaw LAPS password.

let startDate = ago(30d);
let endDate = now();
AuditLogs
| where OperationName == "Recover device local administrator password"
  and TimeGenerated between (startDate .. endDate)
| summarize arg_max(TimeGenerated, *) by CorrelationId
| extend parsedInitiatedBy = parse_json(InitiatedBy)
| extend UserPrincipalName = tostring(parsedInitiatedBy.user.userPrincipalName)
| extend parsedTargetResources = parse_json(TargetResources)
| mv-expand parsedTargetResources // Expand the array into individual rows
| extend DeviceName = tostring(parsedTargetResources.displayName)
| summarize DeviceCount = dcount(DeviceName) by UserPrincipalName
| project UserPrincipalName, DeviceCount
| order by DeviceCount desc